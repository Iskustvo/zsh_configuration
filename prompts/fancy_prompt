#!/bin/zsh

######################################################## COLORS ########################################################

# Default color/style.
NO_COLOR=$'%{\e[0m%}'

# Color for regular text inside of prompt.
TEXT_COLOR=$'%{\e[38;2;0;0;0m%}'

# Colors for icons.
FAILED_COLOR=$'%{\e[1;38;2;255;0;0m%}'
JOBS_COLOR=$'%{\e[1;38;2;255;255;255m%}'

# Prompt colors based on Vim modes.
VIM_NORMAL_PROMPT_RGB='0;200;100'
VIM_NORMAL_SEPARATOR_RGB='0;150;75'
VIM_INSERT_PROMPT_RGB='0;100;200'
VIM_INSERT_SEPARATOR_RGB='0;75;150'

# Git prompt colors based on git status.
GIT_CONFLICT_RGB='255;0;0'
GIT_CHANGED_RGB='200;150;0'
GIT_UNCHANGED_RGB='0;255;0'

######################################################### MAIN #########################################################

# Choose prompt color based on current Vim mode.
case "${VIM_MODE}" in
    normal)
            PROMPT_BG=$'%{\e[0;48;2;'"${VIM_NORMAL_PROMPT_RGB}"'m%}'
            PROMPT_FG=$'%{\e[38;2;'"${VIM_NORMAL_PROMPT_RGB}"'m%}'
            SEPARATOR_BG=$'%{\e[0;48;2;'"${VIM_NORMAL_SEPARATOR_RGB}"'m%}'
            SEPARATOR_FG=$'%{\e[38;2;'"${VIM_NORMAL_SEPARATOR_RGB}"'m%}'
            ;;
    *)
            PROMPT_BG=$'%{\e[0;48;2;'"${VIM_INSERT_PROMPT_RGB}"'m%}'
            PROMPT_FG=$'%{\e[38;2;'"${VIM_INSERT_PROMPT_RGB}"'m%}'
            SEPARATOR_BG=$'%{\e[0;48;2;'"${VIM_INSERT_SEPARATOR_RGB}"'m%}'
            SEPARATOR_FG=$'%{\e[38;2;'"${VIM_INSERT_SEPARATOR_RGB}"'m%}'
            ;;
esac

# Build components for main prompt.
LAST_EXIT_STATUS="${PROMPT_BG}%(0?,,%(148?,, ${FAILED_COLOR}))"
BACKGROUNDED_JOBS=" ${JOBS_COLOR}%(1j,%(2j,,) ,)"
USER_INFO="${PROMPT_BG}${TEXT_COLOR}%n "
SEPARATOR="${SEPARATOR_BG}${PROMPT_FG}${PROMPT_BG}${SEPARATOR_FG}"
DIRECTORY=" ${TEXT_COLOR}$(basename "$(pwd | sed "s|^${HOME}||")" | sed '/^[^]/ s/.*/ &/') "

# Build git prompt.
GIT_DIR="$(git rev-parse --git-dir 2>/dev/null)"
if [[ -z "${GIT_DIR}" ]]; then
    # Not a git repository => Just finish main prompt.
    GIT_PROMPT="${NO_COLOR}${PROMPT_FG}${NO_COLOR} "
else
    # Get branch name(or first 7 digits of checkouted SHA) and set the appropriate symbol.
    if [[ -d "${GIT_DIR}/rebase-merge" ]]; then
        # Interactive rebase.
        BRANCH="$(basename "$(< "${GIT_DIR}/rebase-merge/head-name" )")"
        BRANCH="${BRANCH:/detached HEAD/"$(head -c 7 "${GIT_DIR}/rebase-merge/orig-head")"}"
        SYMBOL=''
    elif [[ -d "$(git rev-parse --git-path rebase-apply)" ]]; then
        # Rebase.
        BRANCH="$(basename "$(< "${GIT_DIR}/rebase-apply/head-name" )")"
        BRANCH="${BRANCH:/detached HEAD/"$(head -c 7 "${GIT_DIR}/rebase-apply/orig-head")"}"
        SYMBOL=''
    elif [[ -f "$(git rev-parse --git-path MERGE_HEAD)" ]]; then
        # Merge.
        BRANCH="$(basename "$(< "${GIT_DIR}/HEAD" )")"
        [[ "${BRANCH}" =~ ^[0-9a-f]{40}$ ]] && BRANCH="${BRANCH:0:7}"
        SYMBOL=''
    elif [[ -f "$(git rev-parse --git-path CHERRY_PICK_HEAD)" ]]; then
        # Cherry-pick.
        BRANCH="$(basename "$(< "${GIT_DIR}/HEAD" )")"
        [[ "${BRANCH}" =~ ^[0-9a-f]{40}$ ]] && BRANCH="${BRANCH:0:7}"
        SYMBOL=''
    else
        # Nothing important in progress.
        BRANCH="$(basename "$(< "${GIT_DIR}/HEAD" )")" && SYMBOL=''
        [[ "${BRANCH}" =~ ^[0-9a-f]{40}$ ]] && BRANCH="${BRANCH:0:7}" && SYMBOL=''
    fi

    # Decide the background color based on current status.
    if [[ -n "$(git ls-files --unmerged)" ]]; then
        # Stuck in the middle of rebase|merge|cherry-pick.
        GIT_BG=$'%{\e[0;48;2;'"${GIT_CONFLICT_RGB}"'m%}'
        GIT_FG=$'%{\e[0;38;2;'"${GIT_CONFLICT_RGB}"'m%}'
    elif ! git diff-index --quiet "$(git write-tree)" -- || [[ -n "$(git ls-files -o --exclude-standard)" ]]; then
        # There are uncommited changes or untracted files.
        GIT_BG=$'%{\e[0;48;2;'"${GIT_CHANGED_RGB}"'m%}'
        GIT_FG=$'%{\e[0;38;2;'"${GIT_CHANGED_RGB}"'m%}'
    else
        # No changes.
        GIT_BG=$'%{\e[0;48;2;'"${GIT_UNCHANGED_RGB}"'m%}'
        GIT_FG=$'%{\e[0;38;2;'"${GIT_UNCHANGED_RGB}"'m%}'
    fi

    # Create git prompt from gathered informations.
    GIT_PROMPT="${GIT_BG}${PROMPT_FG} ${TEXT_COLOR}${SYMBOL} ${BRANCH} ${GIT_FG}${NO_COLOR} "
fi

# Print the whole prompt.
echo "${LAST_EXIT_STATUS}${BACKGROUNDED_JOBS}${USER_INFO}${SEPARATOR}${DIRECTORY}${GIT_PROMPT}"
